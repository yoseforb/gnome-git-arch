# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gsettings-desktop-schemas
pkgname=$_pkgname-git
pkgver=3.9.90.3.g7cacc89
pkgrel=1
_realver=3.9.90
pkgdesc="Shared GSettings schemas for the desktop"
arch=('any')
url="http://live.gnome.org/"
license=('GPL')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
install=gsettings-desktop-schemas.install
conflicts=('gsettings-desktop-schemas')
replaces=('gsettings-desktop-schemas')
provides=('gsettings-desktop-schemas' "$_pkgname=$_realver")
source=('git://git.gnome.org/gsettings-desktop-schemas')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/gsettings-desktop-schemas"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/gsettings-desktop-schemas"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/gsettings-desktop-schemas"
  make DESTDIR="$pkgdir" install
}
