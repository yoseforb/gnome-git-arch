# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=at-spi2-core
pkgname=$_pkgname-git
pkgver=AT_SPI2_CORE_2_9_90.1.gec424c4
pkgrel=1
_realver=2.9.90
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus' 'glib2' 'libxtst')
makedepends=('gtk-doc' 'git' 'intltool' 'gobject-introspection')
options=('!libtool')
replaces=('at-spi2-core')
provides=('at-spi2-core' "at-spi2-core=$_realver")
conflicts=('at-spi2-core')
source=(git://git.gnome.org/at-spi2-core)
sha256sums=('SKIP')

 pkgver() {
  cd "$srcdir/at-spi2-core"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/at-spi2-core"

  sed -i -e '/AC_PATH_XTRA/d' configure.ac

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie \
    --enable-gtk-doc
  make
}

package() {
  cd "$srcdir/at-spi2-core"
  make DESTDIR="$pkgdir" install
}
